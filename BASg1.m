    /// Булгаков А.С.; Игра "Пьяница"
MAIN ; Основная процедура
    n name,author,crd,rul
    s name="КАРТОЧНАЯ ИГРА ПЬЯНИЦА"
    s author="Разработка ООО НПЦ АИР"
    ; карты
    s crd(1)="6Ч;6",crd(2)="6Б;6",crd(3)="6П;6",crd(4)="6K;6"
    s crd(5)="7Ч;7",crd(6)="7Б;7",crd(7)="7П;7",crd(8)="7K;7"
    s crd(9)="8Ч;8",crd(10)="8Б;8",crd(11)="8П;8",crd(12)="8K;8"
    s crd(13)="9Ч;9",crd(14)="9Б;9",crd(15)="9П;9",crd(16)="9K;9"
    s crd(17)="10Ч;10",crd(18)="10Б;10",crd(19)="10П;10",crd(20)="10K;10"
    s crd(21)="ВЧ;11",crd(22)="ВБ;11",crd(23)="ВП;11",crd(24)="ВK;11"
    s crd(25)="ДЧ;12",crd(26)="ДБ;12",crd(27)="ДП;12",crd(28)="ДK;12"
    s crd(29)="КЧ;13",crd(30)="КБ;13",crd(31)="КП;13",crd(32)="КK;13"
    s crd(33)="ТЧ;14",crd(34)="ТБ;14",crd(35)="ТП;14",crd(36)="ТK;14"
    ; правила
    s rul(1)="Количество карт в колоде: 36"
    s rul(2)="Старшинство карт: 6,7,8,9,10,В,Д,К,Т"
    s rul(3)="Цель игры: собрать все карты у себя"
    s rul(4)="Правила игры:"
    s rul(5)="Сдатчик карт определяется жребием"
    s rul(6)="Карты в колоде тщательно тасуются колода раздается"
    s rul(7)="поровну между игроками. Игроки не смотрят своих карт,"
    s rul(8)="а кладут их стопкой в закрытом виде возле себя."
    s rul(9)="Первый ход принадлежит сдатчику карт, он снимает"
    s rul(10)="свою верхнюю карту, открывает и кладет на центр стола"
    s rul(11)="в открытом виде. Другие игроки по кругу по часовой"
    s rul(12)="стрелке делают то же самое. Затем смотрят чья карта"
    s rul(13)="старше, тот игрок у которого карта старше, забирает карты"
    s rul(14)="и кладет их в том же порядке под низ своей колоды."
    s rul(15)="Если у игроков окажутся равные старшие карты,"
    s rul(16)="то забирает тот игрок, который первый положит эту карту,"
    s rul(17)="а первый кладет тот, кто по часовой стрелке находится"
    s rul(18)="левее другого игрока. Далее ход переходит к следующему"
    s rul(19)="игроку по часовой стрелке. Если у игрока заканчиваются "
    s rul(20)="карты, то он выбывает из игры. Выигрывает игрок, который"
    s rul(21)="соберет все карты у себя."
    d ECHOOFF
    ; управление
    f  q:$$C1()
    d CLRSCR
    d ECHOON
    q
TOP ; Шапка
    w " ",name,!
    w " ",author,!
    q
CLRSCR ; Очистка экрана
    w #
    q
ECHOON ; Включить ECHO
    u 0:(0:"")
    q
ECHOOFF ; Выключить ECHO
    u 0:(0:"SI")
    q
    /// Обрабокта нажатий клавиш и управление
    /// основным меню
C1()
    d CLRSCR
    d TOP
    w !," Управление:",!
    w " F1:Правила; F2:Начать игру; ESC:Выход;",!
    s k=$$Key()
    i k="ESC" q 1
    i k="F1" d RULE
    i k="F2" d START
    q 0
RULE ; Правила
    ; ls - кол-во строк rul
    ; ps - всего страниц
    n lm,ls,ps,p
    s p=1,lm=13
    s ls=$o(rul(""),-1)
    s ps=ls\lm+1 
    f  q:$$RL()
    q
    /// Управление выводом правил
    /// по страницам
RL()
    d CLRSCR
    d TOP
    w !," Правила",!
    ; проверка страницы
    s:p>ps p=ps
    s:p<1 p=1
    w " Страница:"_p_"/"_ps,!
    f i=1:1:lm w " ",$g(rul(p-1*lm+i)),!
    w !," ESC:Выход; LEFT:Назад; RIGHT:Вперед;",!
    s k=$$Key() 
    i k="ESC" q 1
    i k="LEFT" s p=p-1
    i k="RIGHT" s p=p+1
    q 0
START ; Новая игра
    ; mv-вес,uk-карта
    ; ui-игрок,st-карты
    n game,qs,un,ud,gi
    n cu,mv,uk,ui,st
    d CLRSCR
    d TOP
    w !," Новая игра",!
    s ex="1(1""0"",1""1"")"
    s ex="1(1""2"",1""3"",1""4"",1""5"",1""6"")"
    s qs=$$Qw(" Игроков[2-6](2)?: ",ex,2)
    d MIX
    ; добавление игроков
    f i=1:1:qs d adUser(i)
    ; раздача карт
    f i=1:1:36 d Uc(i)
    f  q:$$Game()
    q
PRG ; Заголовок
    d CLRSCR
    d TOP
    w !," Игра:",!
    f i=1:1:qs w " #"_i_": "_un(i),!
    q
MIX ; Перемешиваем
    f i=1:1:36 d Move($r(36-i+1)+1,i)
    q
    /// Перестановка 2-х карт
    /// [Параметры]
    /// J* - карта 1
    /// I* - карта 2
Move(I,J)
    s t=crd(I)
    s crd(I)=crd(J)
    s crd(J)=t
    q
    /// Вопросы / Ответы
    /// [Параметры]
    /// Qw - вопрос
    /// Exp - шаблон
    /// Def - значение по умолчанию
    /// [Описание]
    /// Обработка вопросов и ответов
    /// с проверкой ответа по шаблону
    /// Если ничего не введено используеться
    /// значение по умолчанию
Qw(Qw,Exp,Def)
    d ECHOON
    w !,Qw
    r in
    i '$l(in) s in=$g(Def) w in
    d ECHOOFF
    i in?@Exp q in
    q $$Qw(Qw,Exp)
    /// Добавление игрока
    /// [Параметры]
    /// I* - номер игрока
adUser(I)
    s m=" Игрок("_I_")[3-10]:"
    s un(I)=$$Qw(m,"3.10E","user"_I)
    q 1
    /// Раздача карт игрокам
    /// [Параметры]
    /// I* -номер карты
Uc(I)
    s j=I#qs
    s:'j j=qs
    d setUc(j,crd(I))
    q
    /// Добавление карты
    /// [Параметры]
    /// I* -номер игрока
    /// V* -карта
setUc(I,V)
    n id
    s id=$o(ud(I,""),-1)+1
    s ud(I,id)=V
    q
    /// Шаг игры
Game()
    k cu,eq,mv,st
    s mv=0,gi=$i(gi),eq=0
    ; cu - кол во игроков
    ; mv - максимальный вес карты
    d CLRSCR
    d TOP
    d CNTALL
    w !," Круг:",gi
    w !," Карты: "
    s i="" f  s i=$o(ud(i)) q:i=""  d
    . w "№",i,"="_ud(i)_";"
    w !," Ход",!
    s i="" f  s i=$o(ud(i)) q:i=""  d UC(i)
    ; поиск игроков с похожими картами
    ; по сохранению в st
    s v="" f  s v=$o(st(v)) q:v=""  d EQ(v)
    ; дополнительный ход
    i eq>1 d DS
    w " Старшая карта: "_uk,!
    w " карты забирает: "_un(ui),!
    d ADC
    d CNTALL
    q:cu<2 $$Win
    w !," Управление:",!
    w " ESC:Выход; *:Продолжить",!
    s k=$$Key()
    i k="ESC" q 1
    q 0
DS ; дополнительный ход
    s i="",mv=0 
    w " +Ход",!
    f  s i=$o(eq(i)) q:i=""  d UC(i)
    q
    /// Карта игрока
    /// [Параметры]
    /// I* - номер игрока
    /// [Описание]
    /// Обработка первой по
    /// младшему индексу карты
UC(I)
    n i,j,v,m,w
    s j=$o(ud(I,""))
    q:j=""
    s v=$g(ud(I,j)) ; карта
    s m=$p(v,";",1) ; масть
    s w=$p(v,";",2) ; вес
    i w>mv s mv=w,uk=m,ui=I
    s st(v)=I ; сохранили на стол
    k ud(I,j) ; убрали у игроков
    w " №",I," ",un(I)," карта: "_m,!
    q
    /// Вывод карт игрока
    /// [Параметры]
    /// I*-номер игрока
UCLS(I)
    n i,j,v
    s j="" f  s j=$o(ud(i,j)) q:j=""  d
    . s v=$g(ud(i,j)) w $p(v,";",1),";"
    w !
    q
    /// Поиск игроков с похожими картами
    /// [Параметры]
    /// V*-значение в сохранении
EQ(V)
    s w=$p(V,";",2)
    q:w'=mv
    s eq=$i(eq)
    s i=$g(st(V))
    s eq(i)=1
    q
    /// Победитель
Win()
    w !," Победитель: ",$g(un(ui)),!
    w " Спасибо за игру!",!!
    r " *:Продолжить",!,*k
    q 1
CNTALL ; Пересчет карт
    s cu=0
    f i=1:1:qs d CNT(i)
    q
    /// Пересчет карт
    /// [Параметры]
    /// I* - номер игрока
CNT(I)
    s kc=0,n=""
    f  s n=$o(ud(I,n)) q:n=""  s kc=$i(kc)
    i 'kc k ud(I) q
    s ud(I)=kc
    s cu=$i(cu) 
    q
ADC ; Добавление карт победителю
    s v=""
    f  s v=$o(st(v)) q:v=""  d setUc(ui,v)
    k st
    q
    /// Получения нажатой клавишы
    /// [Описание]
    /// Если клавиша входит в escape
    /// для ней возврашается название
    /// иначе возвращается код
Key()
    n k,h,b
    s b(3)="CTRL+C"
    s b(8)="BS"
    s b(9)="TAB"
    s b(13)="ENT"
    s b(21)="CAN"
    s b(27)="ESC"
    s b(32)="SPACE"
    s b(127)="DEL"
    s b(27_91_49_126)="HOME"
    s b(27_91_51_126)="INS"
    s b(27_91_51_126)="DEL"
    s b(27_91_52_126)="END"
    s b(27_91_53_126)="PAGE-UP"
    s b(27_91_54_126)="PAGE-DOWN"
    s b(27_91_65)="UP"
    s b(27_91_66)="DOWN"
    s b(27_91_67)="RIGHT"
    s b(27_91_68)="LEFT"
    s b(27_79_80)="F1"
    s b(27_79_81)="F2"
    s b(27_79_82)="F3"
    s b(27_79_83)="F4"
    s b(27_79_84)="F5"
    s b(27_91_49_55_126)="F6"
    s b(27_91_49_56_126)="F7"
    s b(27_91_49_57_126)="F8"
    s b(27_91_50_48_126)="F9"
    s b(27_91_50_49_126)="F10"
    s b(27_91_50_51_126)="F11"
    s b(27_91_50_52_126)="F12"
    r *k
    f  s h=$$byte() s:h k=k_h q:'h
    s k=$g(b(k),k)
    w " >",k
    q k
byte() ; Чтение 10 байт
    f i=1:1:10 r *h:0 q:h'<0
    q:h'<0 h
    q 0